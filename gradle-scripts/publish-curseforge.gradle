// Publish to CurseForge
curseforge {
    if (project.curse_api_key) {
        apiKey = project.curse_api_key
        project {
            id = project.curse_project_id
            releaseType = 'release'
            changelogType = 'markdown'
            changelog = rootProject.file("changelog.md").text
            addGameVersion project.minecraft_version
            addGameVersion project.loader_platform

            mainArtifact(remapJar.archiveFile.get().asFile) {
                relations {
                    requiredDependency 'valkyrien-skies'
                    requiredDependency 'cc-tweaked'
                    if (project.loader_platform == "Forge")
                        requiredDependency 'kotlin-for-forge'
                }
                displayName = 'CC: VS ' + project.minecraft_version + ' ' + project.loader_platform + ' ' + project.mod_version
            }
        }
    }
    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}

// Publish to Modrinth
modrinth {
    token = project.modrinth_api_key
    projectId = project.modrinth_project_id
    versionType = "release"
    versionName = 'CC: VS ' + project.minecraft_version + '-' + project.loader_platform.toLowerCase() + '-' + project.mod_version
    versionNumber = project.mod_version
    uploadFile = remapJar.archiveFile.get().asFile
    gameVersions = [project.minecraft_version]
    loaders = [project.loader_platform.toLowerCase()]
    changelog = rootProject.file("changelog.md").text

    dependencies {
        required.project "valkyrien-skies"
        required.project "cc-tweaked"
        if (project.loader_platform == "Forge")
            required.project "kotlin-for-forge"
    }
}
